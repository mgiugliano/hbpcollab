begintemplate HC_model

public init, biophys, geom_nseg, create_model

public soma, dend, apic, axon,myelin
public all, somatic, apical, axonal, basal, nSecSoma, nSecApical, nSecBasal, nSecAxonal, nSecAll, nSecAxonalOrig

objref all, somatic, apical, axonal, basal, this

strdef tstr

proc init() {
	all = new SectionList()
	somatic = new SectionList()
	basal = new SectionList()
	apical = new SectionList()	
	axonal = new SectionList()
 	forall delete_section()
}


create soma[1], dend[1], apic[1], axon[1],myelin[1]


proc geom_nseg() {local nSec, L1, L2, D1, D2, nSeg1, nSeg2
  soma area(.5) 
  nSec = 0
  forsec all {
  nseg = 1 + 2*int(L/40)
  nSec = nSec + 1
  }
  nSecAll = nSec
  nSec = 0
  forsec somatic { nSec = nSec + 1}
  nSecSoma	= 	nSec
  nSec = 0
  forsec apical { nSec = nSec + 1}
  nSecApical= 	nSec
  nSec = 0
  forsec basal { nSec = nSec + 1}
  nSecBasal	= 	nSec
  nSec = 0
  forsec axonal { nSec = nSec + 1}
  nSecAxonalOrig = nSecAxonal	= 	nSec
}



proc biophys() {
	forsec all {
	
 insert pas
		g_pas = 0.000033
		cm = 0.75
		Ra = 100
		e_pas = -70
	}

	
	forsec somatic {
	insert na
	insert kv
	ek = -85
	ena = 50
	 gbar_na = 800
	 gbar_kv = 320

	}
	forsec apical {
		cm = 0.75+0.75*14.19/10*0.59 //according to BENAVIDES-PICCIONE et al 2002
		g_pas = 0.0000606 //g_pas = 1/rm = 1/(taum/cm)
		insert na
		insert kv
		ek = -85
		ena = 50
	 	gbar_na = 20
	 	gbar_kv = 10
	 
	 }
	 
	 forsec basal {
	 	cm = 0.75+0.75*14.19/10*0.59 //according to BENAVIDES-PICCIONE et al 2002
		g_pas = 0.0000606 //g_pas = 1/rm = 1/(taum/cm)
		insert na
		insert kv
		ek = -85
		ena = 50
	 	gbar_na = 20
	 	gbar_kv = 10
	 
	 }
	 
	access axon[0]
	insert na
	insert kv	
	ek = -85
	ena = 50
	gbar_na = 8000
	gbar_kv = 1500

	access myelin
	cm = 0.02
	Ra = 100
	g_pas = 6.6e-7
	e_pas = -70
	

	
	access soma	
    celsius = 37
	
}


proc create_model(){
    forsec axonal{delete_section()}
    
	create axon[1]
	access axon
	nseg=25
	L=50
	diam =1
	axonal.append()
	all.append()
	connect axon(0) , soma(1)
		
	create myelin[1]
	access myelin
		L = 1000
		diam = 1
		nseg = 1+2*int(L/100)
	all.append()
	connect myelin(0), axon[0](1) 
	
	access soma

}

endtemplate HC_model
